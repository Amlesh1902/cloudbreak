import org.ajoberstar.grgit.*

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.8.0'
    }
}

def grgit = Grgit.open(project.file('.'))
def buildNumber = System.getenv("BUILD_NUMBER")
def branch = grgit.branch.current.name
if ("HEAD".equals(branch)){
    branch = System.getenv("GIT_BRANCH")
}

version = new ProjectVersion(0, 1, buildNumber != null ? buildNumber : "dev", branch)

class ProjectVersion {
    Integer major
    Integer minor
    String build
    String branch

    ProjectVersion(Integer major, Integer minor, String build, String branch) {
        this.major = major
        this.minor = minor
        this.build = build
        this.branch = branch
    }

    @Override
    String toString() {
        String fullVersion = "$major.$minor"
        if(build) {
            fullVersion += "-$build"
        }
        if (branch != null && !branch.contains("master")) {
            fullVersion += "-$branch"
        }
        fullVersion
    }
}