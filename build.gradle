buildscript {
  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'org.ajoberstar:grgit:2.1.0'
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
    classpath 'org.ajoberstar.reckon:reckon-gradle:0.4.0'
    classpath 'com.smokejumperit.gradle.license:Gradle-License-Report:0.0.2'
  }
}

plugins {
  id 'org.ajoberstar.grgit' version '2.1.0'
}

if (project.hasProperty("reckon.scope")) {
  apply plugin: "org.ajoberstar.reckon"
  reckon {
    normal = scopeFromProp()
    preRelease = stageFromProp('dev', 'rc', 'final')
  }
}

def env = project.hasProperty('env') ? project.getProperty('env') : 'local'
ext.config = new ConfigSlurper(env).parse(file("$rootDir/gradle/config/buildConfig.groovy").toURL())

allprojects {
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  group = 'com.sequenceiq'
  configurations {
    testRuntime {
      exclude group: "org.springframework.cloud"
    }
  }

}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  configurations {
    deployerJars
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'com.google.guava', module: 'guava-jdk5'
  }

  dependencies {
    deployerJars("org.springframework.build.aws:org.springframework.build.aws.maven:3.0.0.RELEASE")
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "s3://cloudbreak-maven/releases") {
          authentication(userName: "$System.env.AWS_ACCESS_KEY", passphrase: "$System.env.AWS_SECRET_ACCESS_KEY")
        }
        snapshotRepository(url: "s3://cloudbreak-maven/snapshots") {
          authentication(userName: "$System.env.AWS_ACCESS_KEY", passphrase: "$System.env.AWS_SECRET_ACCESS_KEY")
        }
      }
    }
  }

  test.testLogging.showStandardStreams = project.hasProperty('showStandardStreams')

  task allDeps(type: DependencyReportTask) {}

  repositories {
    mavenCentral()
    maven { url "https://cloudbreak-maven.s3.amazonaws.com/releases" }
    maven { url "https://repo.spring.io/release" }
    maven { url "https://plugins.gradle.org/m2/" }
    mavenLocal()
  }
}